---
title: OSC
tags: [osc, oscdef, slider, register]

---

See <http://doc.sccode.org/Guides/OSC_communication.html> for an overview.

In SuperCollider by default the server listens for OSC messages on port __57110__,
and the client on port __57120__.

There is a convenience method `OSCFunc.trace` which posts all incoming OSC messages:

~~~
OSCFunc.trace(true);   // turn it on
OSCFunc.trace(false);  // turn it off
~~~

Messages received have the following info:
~~~
	time: 1529001227.3191
	address: a NetAddr(192.168.1.2, 59001)
	recvPort: 57120
	msg: [ /1/push2, 0 ]
~~~

##### `OSCdef`

We create an OSCdef for every path we want to respond to:

~~~
(
OSCdef.new(\button, {
    arg msg, time, addr, port;
    msg[1].postln;                 // msg[0] contains the path, msg[1] contains the value
}, '/1/push1');
)

OSCdef(\button).free;
~~~

##### Sending OSC messages to the server

In general we won't need to, since *sclang* allows us to interact
with the server in a much more practical way.

~~~
x = Synth.new(\tone);
x.set(\gate, 0);
x.free
~~~

The equivalent in OSC would be (note that we must provide and keep track of the node id):

~~~
s.sendMsg('/s_new', \tone, 2345, 0, 1);    // node id, add action (0: add to head), target id
s.sendMsg('/n_set', 2345, \gate, 0);
s.sendMsg('/n_free', 2345);
~~~

See <http://doc.sccode.org/Reference/Server-Command-Reference.html>

##### Receiving OSC messages from the server

When we start sclang, it is already registered for receiving the local server notifications
(if we wanted to register to another server, we would send it the `/notify` message).

We then use the `SendReply` Ugen as follows:

~~~

(
SynthDef.new(\ocean, {
	var sig, mod;
	sig = BrownNoise.ar(0.15!2);
	mod = SinOsc.kr(0.2, 3pi/2).exprange(0.002, 1);
	sig = sig * mod;
	sig = sig + (0.2*GVerb.ar(sig, 200, 5));
	SendReply.kr(Impulse.kr(20), '/ocean_amp', mod);  // send the mod amplitude 20 times/sec
	Out.ar(0, sig);
}).add;
)

Synth(\ocean);

(
w = Window.new("Waves", Rect(1100, 1400, 300, 300)).front.alwaysOnTop_(true);
~slider = Slider.new(w, Rect(20, 20, 20, 200)).enabled_(false);
)

(
OSCdef.new(\trackOcean, {
	arg msg;
	msg.postln;
	{~slider.value_(msg[3])}.defer;    // or AppClock.sched(0, {~slider.value_(msg[3])})
}, '/ocean_amp');
)

~~~


##### Registering synths

Just as we saw with our [GUI objects](/post/gui), when using `OSCdef` to control our synths we'll have errors
if we try to set parameters on non-existing synths.

As an alternative to the solution of targeting groups that we saw earlier, we can register the synth.

Synths inherit from nodes the methods `isPlaying` and `isRunning`. For these methods to return a valid
value we must first register out synth at the `NodeWatcher` by calling `register`.

~~~
(
x = new Synth(\sound);
x.register;
)
~~~

Then in our `OSCdef` function:

~~~
(
OSCdef.new(\slider, {
    arg msg, time, addr, port;
    if (x.isPlaying,
    	{ x.set(\freq, msg[1].linexp(0, 1, 200, 2000)) }
    );
}, '/1/slide1');
)
~~~
