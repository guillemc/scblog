---
title: Busses and Groups
tags: [node, group, bus]

---

#### Busses

Busses allow us to route signals.

There are 2 types of busses: audio-rate and control-rate.

Audio rate busses are special because they include the output and input audio channels.

For example, if our server is configured to have 2 output channels and 2 input channels, the first 4 audio rate busses will refer to these channels:

- [0, 1]: output channels
- [2, 3]: input channels

The remaining audio rate busses (and all control rate busses) are private and can be used for sending signals between UGens.

Unlike audio rate busses, control rate busses hold their last value until something new is written.

#### Writing and reading

To write to a bus, we use the `Out` UGen:

~~~
Out.ar(0, sig);          // write sig to the audio bus 0
Out.ar(0, [sig1, sig2]); // write the signal array starting from bus 0
~~~


To read from a bus, we use the `In` UGen:

~~~
In.ar(0, 1);      // read from bus 0
In.ar(0, 4);      // read 4 channels starting from bus 0
~~~

#### Creating busses


When allocating an audio bus, we can be sure it will be a private one (i.e. not any input or output channel):

~~~
a = Bus.audio(s);       // get one audio bus from s (local server)
~~~

We can allocate multiple busses at a time, which in practice is like having multi-channel busses:

~~~
c = Bus.control(s, 2);   // get 2 control busses (~ 2-channel control bus)
~~~

Example: a reverb synth that listens for mono signals on one bus and outputs its stereo output on another bus:

~~~

(
SynthDef.new(\tone, {
	arg freq = 1000, out = 0;
	var sig, env;
	sig = SinOsc.ar(freq);
	env = EnvGen.kr(
		Env.new([0, 1, 0], [0.001, 0.1], [1, -1]),
		LFSaw.kr(0.5);
	);
	sig = sig * env * 0.3;
	Out.ar(out, sig);
}).add;

SynthDef.new(\reverb, {
	arg in = 0, out = 0;
	var sig;
	sig = In.ar(in, 1);
	sig = sig + (GVerb.ar(sig, 200, 5) * 0.2); // note: GVerb outputs a stereo signal
	Out.ar(out, sig);
}).add;
)

~reverbBus = Bus.audio(s, 1);

~reverbSynth = Synth(\reverb, [\in, ~reverbBus, \out, 0]);

~toneSynth = Synth(\tone, [\freq, exprand(300, 2000), \out, ~reverbBus]);

~~~


#### Nodes and order of execution

The order of execution is important.

In the node tree, the tone synth should be above the reverb synth.

There are ways to explicitly define the order:

~~~
(

~toneSynth = Synth(
	\tone,
	[\freq, exprand(300, 2000), \out, ~reverbBus],  // params
	s,                 								// target
	\addToHead                                      // position
);

~reverbSynth = Synth(
	\reverb,
	[\in, ~reverbBus, \out, 0],                     // params
	~toneSynth,                                     // target
	\addAfter                                       // position
);

)
~~~

Most commonly we first set up all the effects, and then we add the signal:

(although not really needed because the default action is *\addToHead* to the default group):
~~~
(

~reverbSynth = Synth(\reverb, [\in, ~reverbBus, \out, 0]);
~toneSynth = Synth(\tone,
	[\freq, exprand(300, 2000), \out, ~reverbBus],
	~reverbSynth,
	\addBefore);
)
~~~

#### Groups

We can also use groups for organizing our synths.

We first create the groups, and then all we have to do is make the target of each synth be the group it belongs to:

~~~
(
~sourceGroup = Group.new;
~fxGroup = Group.after(~sourceGroup);

Synth(\reverb, [\in, ~reverbBus, \out, 0], ~fxGroup);
Synth(\tone, [\freq, exprand(300, 2000), \out, ~reverbBus], ~sourceGroup);
Synth(\tone, [\freq, exprand(300, 2000), \out, ~reverbBus], ~sourceGroup);
Synth(\tone, [\freq, exprand(300, 2000), \out, ~reverbBus], ~sourceGroup);
)

~sourceGroup.set(\out, 0);            // send them straight to the output
~sourceGroup.set(\out, ~reverbBus);   // send them back to the reverb bus
~sourceGroup.freeAll;                 // send the free message to every synth in the group (reverb keeps going on)
~~~