---
title: SynthDef and Synth
tags: [functions, synth, play]

---

#### Creating synths

`{ ... }.play` is a convenience method that: wraps the `Function` in a `SynthDef` (adding an `Out` UGen if needed), creates and new `Synth` from the `SynthDef`, starts it, and returns it.

~~~
x = { |freq=320, phase=1.5pi, amp=0.3| SinOsc.ar(freq, phase, amp)!2 }.play;
x.set(\freq, 340)
x.free
~~~

To build a `SynthDef` from a function we give it a name and add an `Out` UGen to the function.
Then we call `add()` to send it to the server.

~~~
(
SynthDef.new(\tone, {
    arg freq=320, phase=1.5pi, amp=0.3;
    var sig = SinOsc.ar(freq, phase, amp);
    Out.ar(0, sig!2);
}).add();
)
~~~

We can now create a `Synth` based on the `SynthDef` just by specifying its name (plus any initial argument values):

~~~
x = Synth.new(\tone, [\freq, 400]);
x.set(\freq, 700);
x.free;
~~~


#### Randomness in synths

The function in a `SynthDef` is evaluated once and only once. This means that random values are calculated only once and are the same for every new `Synth`.

To have different random values for every newly created `Synth` we must use one of the multiple random UGens such as `Rand` or `ExpRand`. These UGens calculate a random value once when the synth starts playing.

~~~
SynthDef(\randtone, { Out.ar(0, SinOsc.ar(ExpRand(200, 2000)!2, 0, 0.2)) }).add;
x = Synth(\randtone);
y = Synth(\randtone);
x.free; y.free;
~~~

Note how the randomly chosen tone is used for both channels, since multichannel expansion is performed on the already chosen frequency.


#### Conditional logic inside a `SynthDef`

`SynthDef` functions are executed in the server, which only handles floating-point numbers coming out of UGens.
To perform conditional logic we must use UGens such as `Select`.

<https://supercollider.github.io/tutorials/If-statements-in-a-SynthDef.html>

