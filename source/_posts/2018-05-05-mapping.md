---
title: Mapping and Clipping
tags: [mapping, range, clip, functions]

---
#### Controlling UGen output range

We can use the `range` and `exprange`
functions instead of using the `mul` and `add` arguments:

~~~
(
{
    var freq;
    freq = SinOsc.ar(0.33, 1.5pi).exprange(88, 880);     // freq from 88 to 880 Hz, exponentially
    SinOsc.ar(freq).range(-0.25, 0.25)!2;                // amplitude from -0.25 to 0.25
}.play;
)
~~~

Note that for `exprange` the value cannot cross zero.


#### Clipping output

The `clip` method limits output values between two values. It works for numbers and arrays:

~~~
-1.5.clip(-1, 1);                       // -1
[ -2, -0.5, 0, 0.5, 2 ].clip(-1, 1);    // [ -1, -0.5, 0, 0.5, 1 ]
~~~

Also works for UGen (it wraps the receiver in a `Clip` UGen):

~~~
{ SinOsc.ar(25).clip(-0.75, 0.75) }.plot(0.1);

{ Clip.ar(SinOsc.ar(25), -0.75, 0.75) }.plot(0.1);  // equivalent
~~~

![clip](/images/blog/clip.png "A clipped sine wave")


#### Mapping functions

These functions work on arrays and also on UGen output.
They take the min and max input values and the min and max output values
they will be mapped to:

~~~
a = (1..10);                     // [ 1, 2, 3, 4, 5, 6, 7, 8, 9, 10 ]
a.linlin(1, 10, 100, 200);       // [ 100, 111.11, 122.22 ... 188.88, 200 ]
a.linexp(1, 10, 100, 200);       // [ 100, 108.01, 116.65 ... 185.17, 200 ]
~~~

With `lincurve` you specify the curvature (0 = line):

~~~
a.lincurve(1, 10, 100, 200, 5);  // [ 100, 100.50, 101.38 ... 157.09, 200 ]
a.lincurve(1, 10, 100, 200, -5); // [ 100, 142.91, 167.54 ... 199.50, 200 ]
~~~