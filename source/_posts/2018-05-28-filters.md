---
title: Filters
tags: [ugen, filter, noise]

---

We'll see the effect of different filter UGens using noise as a source.

#### White noise and Pink noise

`WhiteNoise` generates noise whose spectrum has equal power at all frequencies:

![whitenoise](/images/blog/whitenoise.png "WhiteNoise")

`PinkNoise` generates noise with equal power over the span of each octave.

![pinknoise](/images/blog/pinknoise.png "PinkNoise")

We'll use white noise to see more clearly the effects of the filters.

------------------------------------

### `LPF`: Low pass filter

Takes as arguments an input signal and a cutoff frequency:

~~~
(
{
    var sig = WhiteNoise.ar(0.4);
    LPF.ar(sig, 600);
}.play
)
~~~

![lpf](/images/blog/lpf.png "LPF")

Replace `LPF` with `HPF` to obtain a high pass filter instead.


------------------------------------

### `BPF`: Band pass filter

Takes a signal, a center frequency and a *rq* (reciprocal of *Q*) vale.
*Q* is defined as centerFreq/bandwidth. Higher *Q* values mean narrower filters.

~~~
(
{
    var sig = WhiteNoise.ar(1);
    BPF.ar(sig, 600, 1/40);
}.play
)
~~~

![bpf](/images/blog/bpf.png "BPF")

Since a high *Q* filter results in quieter noise, we can compensate doing
something like:

~~~
(
{
	arg q = 40;
    var sig = WhiteNoise.ar();
    BPF.ar(sig, 600, q.reciprocal, q.sqrt);  // the mul value is adjusted with q
}.play
)
~~~

`BRF` (band reject filter) is the reciprocal of the `BPF`. Use low *Q*
values to obtain a wider rejection band.

------------------------------------

### `RLPF`: Resonant low pass filter

Same arguments as the `BPF`: signal, cutoff frequency and *rq*.

It is a low pass filter that emphasizes the cutoff frequency.

With low *Q* values it behaves just like a low pass filter.

Works great with `Saw` or other signals with lots of harmonics,
as it allows us to emphasize some of those harmonics:

~~~
(
{
    var sig = Saw.ar([60, 61], 0.3);
    RLPF.ar(sig, 600, 1/40);
}.play
)
~~~

![rlpf](/images/blog/rlpf.png "RLPF")


Example: Saw wave through RLPF with random cutoff frequency.

(AllpassN is a delay line with args: input, max delay time, delay time, decay time).

~~~
(
SynthDef(\saw, {
	arg freq;
	var sig, cf;
	cf = LFNoise1.kr(1, 36, 110).midicps;
    sig = LFSaw.ar(freq, 0, EnvGen.kr( Env.perc, levelScale: 0.3, doneAction: 2 ));
    sig = RLPF.ar(sig, cf, 1/10);
    4.do({ sig = AllpassN.ar(sig, 0.05, [0.05.rand, 0.05.rand], 4) });
	Out.ar(0, sig);
}).add;
)


(
Routine.new({
    30.do({
        var synth = Synth.new(\saw, [\freq, [60, 64, 65, 72].choose.midicps]);
        0.2.wait;
    });
}).play;
)
~~~
