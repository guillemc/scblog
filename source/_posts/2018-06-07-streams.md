---
title: Streams and Patterns
tags: [stream, next, routine, task, pattern]

---

#### Streams

- A stream represents a lazy sequence of values.
- The next value in the sequence is obtained by sending the message `next` to the stream object.
- The sequence can be restarted by sending the message `reset` to the stream object.
- A stream can be of finite or infinite length.
- When a finite length stream has reached the end, it returns `nil`.
- Any object is by default a stream that represents an infinite sequence of itself.

~~~
7.next;   // 7 (numbers respond to next by returning themselves)
~~~


#### FuncStream

A `FuncStream` provides a stream that consists of evaluations of its function:

~~~
a = FuncStream.new({ #[1, 2, 3, 4].choose });
a.next;        // get next value
a.nextN(5);    // get 5 next values
~~~

#### Routine

Routines are functions that can yield (return) a value from the middle and then be resumed from that point when called again.

~~~
r = Routine({
    "abcde".yield;
    "fghij".yield;
    "klmno".yield;
    "pqrst".yield;
    "uvwxy".yield;
    "z{|}~".yield;
});

r.next;
~~~

#### Stream operations

We can do math operations on streams to produce other streams.

~~~
a = Routine.new({ 10.do({ arg i; i.yield; }) });
b = Routine.new({ forBy (100, 280, 20, { arg i; i.yield }) });  // 100 to 280 by 20
c = a + b;                                                      // add streams a and b
~~~

Streams respond to the messages `collect`, `select` and `reject` by returning a new Stream.

~~~
a = Routine.new({ 10.do({ arg i; i.yield; }) });   // stream that returns numbers from 0 to 9
b = a.select({ arg item; item.odd; });             // stream that returns the odd values from a
~~~

---------------------------------------------------

#### Patterns

*Patterns define behavior; streams execute it.*

- Patterns are a way to make multiple streams from a single specification.
- Since the stream does not modify the original pattern, any number of streams can be made from the same blueprint
- They respond to the `asStream` message by creating a `Stream`
- Any object is by default a pattern that responds to `asStream` with itself as a stream
- Patterns also respond to math operators by returning patterns that respond to asStream with appropriately modified streams.
- Patterns also respond to the messages `collect`, `select`, and `reject` by returning a new `Pattern`.
- Patterns can be nested: if a Pattern encounters another Pattern in its list, it creates a stream on that pattern and iterates that pattern until it ends before moving on.


##### `Pfunc(nextFunc, resetFunc)`

Pattern that returns a `FuncStream`

~~~
a = Pfunc.new({ #[1, 2, 3, 4].choose });
a.asStream.nextN(7);                     // [ 4, 4, 3, 3, 1, 2, 4 ]
~~~

##### `Prout(routineFunc)`

Pattern that returns a `Routine`

~~~
a = Prout.new({ 1.yield; 2.yield; 7.yield; 3.do { 1.0.rand.yield } });
b = a.asStream.nextN(7);                 // [ 1, 2, 7, 0.467, 0.798, 0.442, nil ]
~~~


##### `Pseq(list, repeats)`

Cycles over a list of values:

~~~
a = Pseq(['a', 'b', 'c'], inf);
a.asStream.nextN(7);                     // [ a, b, c, a, b, c, a ]

a = Pseq(['a', 'b', 'c'], 2);
a.asStream.nextN(7);                     // [ a, b, c, a, b, c, nil ]
~~~

##### `Pshuf(list, repeats)`

Provides values from the list in random, but constant order:

~~~
a = Pshuf(['a', 'b', 'c'], 2);
a.asStream.nextN(7);                     // [ b, a, c, b, a, c, nil ]
~~~

##### `Prand(list, n)`

Provides values from the list at random, n times:

~~~
a = Prand(['a', 'b', 'c'], 6);
a.asStream.nextN(7);                     // [ c, a, b, c, c, c, nil ]
~~~

##### `Pxrand(list, n)`

Like `Prand but never returns the same item twice in a row:

~~~
a = Pxrand(['a', 'b', 'c'], 6);
a.asStream.nextN(7);                     // [ b, c, b, a, c, b, nil ]
~~~


##### `Pwrand(list, weights, n)`

Like `Prand` but chooses values according to a list of probabilities/weights (that should sum 1):

~~~
a = Pwrand(['a', 'b', 'c'], [0.2, 0.6, 0.2], 6);
a.asStream.nextN(7);                     // [ b, b, b, b, c, b, nil ]
~~~

Note: we can use array's `normalizeSum` to provide the weights:

~~~
[10, 2, 1].normalizeSum;                 // [ 0.769, 0.154, 0.077 ]
~~~


##### `Pseries(start, step, length)`

Generates an arithmetic series:

~~~
a = Pseries(10, 2, 6);
a.asStream.nextN(7);                     // [ 10, 12, 14, 16, 18, 20, nil ]
~~~

##### `Pgeom(start, grow, length)`

Generates a geometric series:

~~~
a = Pgeom(10, 2, 6);
a.asStream.nextN(7);                     // [ 10, 20, 40, 80, 160, 320, nil ]
~~~


##### `Pn(pattern, repeats)`

Repeats the pattern as many times as repeats indicates.

Example: order a set of numbers randomly but make sure all numbers come out before a new order is chosen.

~~~
a = Pshuf(['a', 'b', 'c'], 1);
b = Pn(a, 2).asStream;
b.nextN(7);                             // [ c, a, b, a, c, b, nil ]
~~~


##### `Pstutter(n, pattern)`

Repeats each element in the pattern n times (n can be also a pattern).

~~~
a = Pxrand(['a', 'b', 'c'], inf);
b = Pstutter(2, a).asStream;
b.nextN(8);                             // [ c, c, b, b, c, c, a, a ]

~~~


##### Examples

Nesting patterns:

~~~
a = Pseq(#[60, 62, 63, 65]);
b = Pseq([ a, a + 12, a + 7], inf);
b.asStream.nextN(16);                   // [ 60, 62, 63, 65, 72, 74, 75, 77, 67, 69, 70, 72, 60, 62, 63, 65 ]
~~~

~~~
a = Pseq([ 60, Prand(#[63, 65]), 67, Prand(#[70, 72, 74]) ], inf);
a.asStream.nextN(16);                   // [ 60, 65, 67, 72,   60, 63, 67, 74,   60, 63, 67, 74,   60, 65, 67, 70 ]
~~~