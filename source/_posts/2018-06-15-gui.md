---
title: GUI
tags: [gui, window, view, slider, knob, button, lag, group, controlspec]

---

We start by creating a window:

~~~
w = Window.new("Title", Rect(100, 100, 600, 400));  // x, y, width, height
w.front;
w.alwaysOnTop = true;
w.background = Color.rand;

w.close;
~~~

In *sclang* there is an alternative, often more convenient syntax for setters:

~~~
(
w = Window.new
.front
.alwaysOnTop_(true)
.background_(Color.rand);
)
~~~

We can center a window on the screen by doing:

~~~
(
var bounds = Window.screenBounds;
var width = 600;
var height = 400;
var x = (bounds.width/2) - (width/2);
var y = (bounds.height/2) - (height/2);

w = Window.new("Centered", Rect(x, y, width, height)).front;
)
~~~

##### Colors

Colors store R, G, B, A values in float values from 0 to 1.

They can be specified in different ways:

~~~
Color.rand;
Color.white;
Color.gray(0.8);
Color.new(1, 0.5, 0, 0.5);
Color.new255(255, 127, 0, 127);
Color.fromHexString("#FF7F00");    // the # is optional
~~~

##### Views

In general widgets are created by specifying the window to which
they will be attached, and their position/dimension:

~~~
~knob = Knob(w, Rect(10, 10, 40, 40));
~slider = Slider(w, Rect(100, 100, 20, 100));   // vertical slider
~slider_h = Slider(w, Rect(150, 150, 100, 20)); // horizontal slider

~slider_h.remove;                               // removes view from its parent and destroys it

~~~

They have a value (float from 0 to 1), that can be read and set:

~~~
~knob.value;
~knob.value_(0.75);  // updates the knob position
~~~

They have an action method, which takes a function as an argument. This function
will be called when we interact with the widget, and will be passed the widget itself
as an argument:

~~~
(
~slider.action_({
	arg obj;
	obj.value.postln;
});
)
~~~

We can also simulate an interaction through `valueAction` (note that just setting the value
updates the widget but doesn't call the action function):

~~~
~slider.valueAction_(0.25);  // will update position and call action function
~~~

We can interact with the widget that has focus with the mouse or the keyboard.
Some keyboard shortcuts are:

- *r* : `valueAction_(1.0.rand)`
- *n* : `valueAction_(0)`
- *x* : `valueAction_(1)`
- *c* : `valueAction_(0.5)`

##### Controlling synths

When using GUI objects to control our synths, we must be careful not to set a value on a non-existing synth
(for example a synth that just finished playing and has been destroyed).

As a solution for this, we can create groups, add our synths to these groups, and from the GUI action functions we can target those groups rather than the synths themselves.

In this example, note also the `lag` method, which we use to prevent sudden frequency changes (which sound grainy):

~~~
(
g = Group.new;

SynthDef.new(\tone, {
    arg freq = 300, gate = 1;
    var sig, env;
    freq = freq.lag(0.1); // avoid instantaneous freq jumps
    env = EnvGen.kr(Env.asr(0.01, 1, 3), gate, doneAction: 2);
    sig = SinOsc.ar([freq, freq + 2], 0, 0.2);
    sig = sig * env;
    Out.ar(0, sig);
}).add;

~freq = 300;

~button = Button.new(w, Rect(100, 20, 80, 40))
.states_([
    ["off", Color.gray(0.2), Color.gray(0.8)],
    ["on", Color.white, Color.new255(242, 37, 6, 127)]
])
.font_(Font.new(Font.defaultSansFace, 24))
.action_({
    arg obj;
    if (
        obj.value == 1,
        { Synth(\tone, [\freq, ~freq], g); }, // target: group
        { g.set(\gate, 0) }
    );
});
;

~knob = Knob(w, Rect(100, 80, 80, 80))
.value_(~freq.explin(100, 600, 0, 1))
.action_({
    arg obj;
    g.set(\freq, obj.value.linexp(0, 1, 100, 600));
})
;

StaticText(w, Rect(100, 160, 80, 20))
.string_("freq")
.align_(\center)
;

)

~~~


##### Using `ControlSpec`

In the last example we see there is code duplication when mapping the knob value range (0-1)
to our desired freq range (100-600).

We can use a `ControlSpec` to define the mapping once and use it wherever we need:

~~~
(
~spec = ControlSpec(100, 600, \exp);      // 100 to 600 exponentially
~knob = Knob(w, Rect(100, 80, 80, 80))
.value_(~spec.unmap(~freq));
.action_({
    arg obj;
    g.set(\freq, ~spec.map(obj.value));
});
)
~~~