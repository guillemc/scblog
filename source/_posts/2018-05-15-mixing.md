---
title: Mixing and Panning
tags: [mixing, panning, balance, splay, multichannel, ugen, play]

---

#### Mix

Mixes an array of arrays of channels down to a single array of channels. `Mix.new([a, b, c])` is equivalent to `[a, b, c].sum`, which is equivalent to `a + b + c`.


~~~
{ Mix.new(SinOsc.ar([320, 322, 324], 0, 0.2)) }.scope(1);
~~~

Mix an array of arrays of channels down to a single array of channels. `Mix.new([[a, b], [c, d], [e, f]])` is equivalent to `[Mix.new([a, c, e]), Mix.new([b, d, f])]`.

~~~
(
{
    var a, b;
    a = [SinOsc.ar(440, 0, 0.2), Saw.ar(662, 0.2)];
    b = [SinOsc.ar(442, 0, 0.2), Saw.ar(660, 0.2)];
    Mix([a, b]);                                       // equivalent to Mix.new([a, b])
}.scope(2);
)
~~~


The *fill* method sums the channels resulting from evaluating n times a function:

~~~
(
var n = 8;
{
    Mix.fill(n, {
        arg index;
        var freq;
        freq = 320 + (2*index);
        [index, freq].postln;
        SinOsc.ar(freq, 0, 0.9/n);
    });
}.play;
)
~~~


#### Pan2

`Pan2` takes an input signal and a position as arguments, and returns an array of two signals (left/right).

The position goes from -1 (left) to 1 (right), with 0 being the center.

~~~
(
{
    var sig, pos;
    sig = PinkNoise.ar(0.2);
    pos = SinOsc.kr(0.5);     // 2 seconds cycle
    Pan2.ar(sig, pos);
}.play;
)
~~~



#### Balance2

Like `Pan2` but for stereo signals:

~~~
(
{
    var sig, pos;
    sig = [
        SinOsc.ar(500, 0, 0.1),
        LFTri.ar(100, 0, 0.2)
    ];
    pos = LFTri.kr(0.3).range(-0.7, 0.7);
    Balance2.ar(sig[0], sig[1], pos);
}.play;
)
~~~


#### Splay

`Splay` spreads an array of channels across the stereo field.

~~~
(
x = {
    arg spread = 1,   // maximum distribution (0 would be all channels to the center)
        center = 0.0; // center of the distribution (-1 to +1)

    var sig = SinOsc.ar( { |i| LFNoise0.kr(0.5).exprange(50, 500) } ! 10);

    Splay.ar(sig, spread, 0.2, center);
}.play;
)
~~~